import org.gradle.internal.os.OperatingSystem

plugins {
    id 'net.thauvin.erik.gradle.semver' version '1.0.4' 
    id 'org.openjfx.javafxplugin' version '0.0.9'
    id 'idea'
}

apply plugin: 'java-library'
apply plugin: 'application'

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

idea.module.downloadJavadoc = true

application {
    mainClassName = "jpack.Launcher"
}

//The dir under $buildDir for the app image
def packageDir = "jpackage"
//The location of the app dir inside the packageDir
def appDirLinux = "${project.name}/lib/app"
def appDirMacOsX = "${project.name}.app/Contents/app"
def appDirWin = "${project.name}/app"

javafx {
    version = "14"
    modules = ['javafx.controls']
}

sourceCompatibility = '15'
targetCompatibility = '15'

repositories {
    mavenCentral()
    // You may define additional repositories, or even remove "mavenCentral()".
    // Read more about repositories here:
    //   http://www.gradle.org/docs/current/userguide/dependency_management.html#sec:repositories
}

dependencies {
    // https://mvnrepository.com/artifact/org.apache.commons/commons-lang3
    implementation 'org.apache.commons:commons-lang3:3.11'
}

task (version).doLast {
    println "Version: $version"
}

tasks.build.doLast {
    println "Version: $version"
}

private static Iterable<String> osAdaptiveCommand(List<String> commands) {
    def newCommands = []
    if (OperatingSystem.current().isWindows()) {
        newCommands = ['cmd', '/c']
    }

    newCommands.addAll(commands)
    return newCommands
}

//Example files are in src/main/dist so the application plugin picks them up and 
//moves into install dir.
task copyResources(type:Copy) {
    description 'Copy files to the app image before packaging.'
    from "${buildDir}/install/${project.name}/updater"
    if (OperatingSystem.current().isLinux()) {
        into "${buildDir}/${packageDir}/${appDirLinux}/updater"
    } else if (OperatingSystem.current().isMacOsX()) {
        into "${buildDir}/${packageDir}/${appDirMacOsX}/updater"
    } else if (OperatingSystem.current().isWindows()) {
        into "${buildDir}/${packageDir}/${appDirWin}/updater"
    } 
}

task packit(type:Exec, dependsOn:'packitImage') {
    //If project has not evaluated, version will be null
    def arg = [
        'jpackage', 
        '--name', "$project.name", 
        '--app-version', "${-> project.version}",
        '--description', 'Test Description',
        '--dest', "${buildDir}/${packageDir}",
        '--verbose',
        '--vendor', 'Test Vendor'
    ]

    if (OperatingSystem.current().isLinux()) {
        def osArgs = [
            '--app-image', "${buildDir}/${packageDir}/${project.name}",
            '--linux-menu-group', 'Games;',
            '--linux-shortcut'
        ]

        arg.addAll(osArgs)
        commandLine arg

    } else if (OperatingSystem.current().isMacOsX()) {
        def osArgs = [
            '--app-image', "${buildDir}/${packageDir}/${project.name}.app",
        ]

        arg.addAll(osArgs)
        commandLine arg

    } else if (OperatingSystem.current().isWindows()) {
        def osArgs = [
            '--app-image', "${buildDir}/${packageDir}/${project.name}",
            '--win-shortcut',
            '--win-menu',
            '--win-menu-group', 'Games',
            '--win-per-user-install',
            '--type', 'msi'
        ]

        arg.addAll(osArgs)
        commandLine osAdaptiveCommand(arg)

    }  else {
        println "*** Unknown ***"
    }
}

task packitImage(type:Exec, dependsOn: ['clean', 'installDist']) {
    // If project has not evaluated, configured --main-jar does not exist in the 
    // --input directory. Add any additional launchers here.
    project.gradle.projectsEvaluated {
        def arg = [
            'jpackage',
            '--type', 'app-image',
            '--name', "$project.name",
            '--app-version', "$version",            
            '--input', "${buildDir}/install/${project.name}/lib", 
            '--main-class', "$mainClassName",            
            '--main-jar', jar.archiveFileName.get(),
            '--dest', "${buildDir}/${packageDir}"
        ]
        
        commandLine osAdaptiveCommand(arg)
    }
        
}

//Copy the updater to the distribution after the app-image is built. Doing this 
//here keeps the updater off the classpath in the cfg for the main application.
packitImage.finalizedBy copyResources




