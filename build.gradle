import org.gradle.internal.os.OperatingSystem

plugins {
    //Applies application plugin also
    id 'org.beryx.runtime' version '1.8.5'
    id 'net.thauvin.erik.gradle.semver' version '1.0.4' 
    id "org.openjfx.javafxplugin" version "0.0.9"
}

apply plugin: 'java'

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

ext {
   javaMainClass = "packit.ClickMe"
}

application {
    mainClassName = javaMainClass
}

javafx {
    version = "11.0.2"
    modules = [ 'javafx.controls' ]
}

repositories {
    mavenCentral()
    // You may define additional repositories, or even remove "mavenCentral()".
    // Read more about repositories here:
    //   http://www.gradle.org/docs/current/userguide/dependency_management.html#sec:repositories
}

dependencies {

}

jar {
    manifest {
        attributes(
            "Main-Class": javaMainClass
        )
    }
}

runtime {
    launcher {
        noConsole = true
    }
    jpackage {
        outputDir = "distribution"

        if (OperatingSystem.current().isLinux()) {
            installerOptions = [ 
                '--verbose',
                '--vendor', 'Test Vendor',
                '--app-version', "${semver.semver}",
                '--linux-menu-group', 'Games;',
                '--description', 'Test Description',
                '--linux-shortcut'
            ]
        } else if (OperatingSystem.current().isMacOsX()) {
            installerOptions = [
                '--verbose',
                '--vendor', 'Test Vendor',
                '--app-version', "${semver.semver}"
            ]
        } else if (OperatingSystem.current().isUnix()) {
            println "*** Unix ***"
        } else if (OperatingSystem.current().isWindows()) {
            installerType = "msi"

            installerOptions = [
                '--verbose',
                '--vendor', 'Test Vendor',
                '--app-version', "${semver.semver}",
                '--win-shortcut',
                '--win-menu',
                '--win-menu-group', "Client"
            ]
        }  else {
            println "*** Unknown ***"
        }
    }
}

task (version).doLast {
    println "Version: $version"
}
