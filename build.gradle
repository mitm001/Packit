import org.gradle.internal.os.OperatingSystem

plugins {
    //Applies application plugin also
    id 'org.beryx.runtime' version '1.11.3'
    id 'net.thauvin.erik.gradle.semver' version '1.0.4' 
    id 'org.openjfx.javafxplugin' version '0.0.8'
    id 'idea'
}

apply plugin: 'java-library'

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

idea.module.downloadJavadoc = true

application {
//    mainModule = "jpack"    
    mainClassName = "jpack.ClickMe"
}

//java {
//    modularity.inferModulePath = true
//}

javafx {
    version = "14"
    modules = ['javafx.controls']
}

sourceCompatibility = '14'
targetCompatibility = '14'

repositories {
    mavenCentral()
    // You may define additional repositories, or even remove "mavenCentral()".
    // Read more about repositories here:
    //   http://www.gradle.org/docs/current/userguide/dependency_management.html#sec:repositories
}

dependencies {
    // https://mvnrepository.com/artifact/org.apache.commons/commons-lang3
    implementation 'org.apache.commons:commons-lang3:3.11'

}
runtime {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']

    jpackage {
        outputDir = "distribution"

        if (OperatingSystem.current().isLinux()) {
            appVersion = "$version"            
            installerOptions = [ 
                '--verbose',
                '--vendor', 'Test Vendor',
                '--linux-menu-group', 'Games;',
                '--description', 'Test Description',
                '--linux-shortcut'
            ]
        } else if (OperatingSystem.current().isMacOsX()) {
            appVersion = "$version"            
            installerOptions = [
                '--verbose',
                '--vendor', 'Test Vendor',
            ]
        } else if (OperatingSystem.current().isUnix()) {
            println "*** Unix ***"
        } else if (OperatingSystem.current().isWindows()) {
            installerType = "msi"
            appVersion = "$version"
            installerOptions = [
                '--verbose',
                '--vendor', 'Test Vendor',
                '--win-shortcut',
                '--win-menu',
                '--win-menu-group', 'Client',
                '--win-per-user-install'
            ]
        }  else {
            println "*** Unknown ***"
        }
    }
}
task (version).doLast {
    println "Version: $version"
}

tasks.build.doLast {
    println "Version: $version"
}