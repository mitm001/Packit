import org.gradle.internal.os.OperatingSystem

plugins {
    //Applies application plugin also
    id 'org.beryx.jlink' version '2.21.2'
    id 'net.thauvin.erik.gradle.semver' version '1.0.4' 
    id 'org.openjfx.javafxplugin' version '0.0.8'
    id 'idea'
}

apply plugin: 'java'

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

idea.module.downloadJavadoc = true

application {
    mainModule = "jpack"    
    mainClassName = "jpack.ClickMe"
}

java {
    modularity.inferModulePath = true
}

javafx {
    version = "14"
    modules = ['javafx.controls']
}

sourceCompatibility = '11'
targetCompatibility = '11'

repositories {
    mavenCentral()
    // You may define additional repositories, or even remove "mavenCentral()".
    // Read more about repositories here:
    //   http://www.gradle.org/docs/current/userguide/dependency_management.html#sec:repositories
}

dependencies {
    // https://mvnrepository.com/artifact/org.apache.commons/commons-lang3
    implementation 'org.apache.commons:commons-lang3:3.11'

}

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']

    jpackage {
        outputDir = "distribution"

        if (OperatingSystem.current().isLinux()) {
            installerOptions = [ 
                '--verbose',
                '--vendor', 'Test Vendor',
                '--app-version', "${semver.semver}",
                '--linux-menu-group', 'Games;',
                '--description', 'Test Description',
                '--linux-shortcut'
            ]
        } else if (OperatingSystem.current().isMacOsX()) {
            installerOptions = [
                '--verbose',
                '--vendor', 'Test Vendor',
                '--app-version', "${semver.semver}"
            ]
        } else if (OperatingSystem.current().isUnix()) {
            println "*** Unix ***"
        } else if (OperatingSystem.current().isWindows()) {
            installerType = "msi"

            installerOptions = [
                '--verbose',
                '--vendor', 'Test Vendor',
                '--app-version', "${semver.semver}",
                '--win-shortcut',
                '--win-menu',
                '--win-menu-group', 'Client',
                '--win-per-user-install'
            ]
        }  else {
            println "*** Unknown ***"
        }
    }
}

task (version).doLast {
    println "Version: $version"
}
