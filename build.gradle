import org.gradle.internal.os.OperatingSystem

plugins {
    id 'net.thauvin.erik.gradle.semver' version '1.0.4' 
    id 'org.openjfx.javafxplugin' version '0.0.9'
    id 'idea'
}

apply plugin: 'java-library'
apply plugin: 'application'

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

idea.module.downloadJavadoc = true

application {
    mainClassName = "jpack.Launcher"
}

javafx {
    version = "14"
    modules = ['javafx.controls']
}

sourceCompatibility = '14'
targetCompatibility = '14'

repositories {
    mavenCentral()
    // You may define additional repositories, or even remove "mavenCentral()".
    // Read more about repositories here:
    //   http://www.gradle.org/docs/current/userguide/dependency_management.html#sec:repositories
}

dependencies {
    // https://mvnrepository.com/artifact/org.apache.commons/commons-lang3
    implementation 'org.apache.commons:commons-lang3:3.11'
}

task (version).doLast {
    println "Version: $version"
}

tasks.build.doLast {
    println "Version: $version"
}

//jar {
    // Keep jar clean:
//    exclude 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA', 'META-INF/*.MF'

//    manifest {
//        attributes 'Main-Class': 'jpack.Launcher',
//                   'Class-Path': configurations.compileOnly.files.collect { "lib/$it.name" }.join(' ')
//    }
    // How-to add class path:
    //     https://stackoverflow.com/questions/22659463/add-classpath-in-manifest-using-gradle
    //     https://gist.github.com/simon04/6865179
//}

private static Iterable<String> osAdaptiveCommand(String... commands) {
    def newCommands = []
    if (OperatingSystem.current().isWindows()) {
        newCommands = ['cmd', '/c']
    }

    newCommands.addAll(commands)
    return newCommands
}

task packitImage(type:Exec) {
    dependsOn clean
    dependsOn 'installDist'
    project.gradle.projectsEvaluated {
        commandLine osAdaptiveCommand( 
            'jpackage',
            '--type', 'app-image',
            '--name', "${project.name}",
            '--input', "${buildDir}/install/${project.name}/lib", 
            '--main-class', "$mainClassName",            
            '--main-jar', jar.archiveFileName.get(),
            '--dest', "${buildDir}/jpackage")
    }
}

task packit(type:Exec) {
    dependsOn packitImage
    
    project.gradle.projectsEvaluated {
        
        if (OperatingSystem.current().isLinux()) {

            commandLine osAdaptiveCommand(
                'jpackage',
                '--app-image', "${buildDir}/jpackage/${project.name}",
                '--name', "$project.name",
                '--app-version', "${version}",            
                '--description', 'Test Description',      
                '--dest', "${buildDir}/jpackage",
                '--verbose',
                '--vendor', 'Test Vendor',                
                '--linux-menu-group', 'Games;',
                '--linux-shortcut'
            )
        } else if (OperatingSystem.current().isMacOsX()) {
            commandLine osAdaptiveCommand(
                'jpackage',
                '--app-image', "${buildDir}/jpackage/${project.name}.app",
                '--name', "$project.name",
                '--app-version', "${version}",            
                '--description', 'Test Description',      
                '--dest', "${buildDir}/jpackage",
                '--verbose',
                '--vendor', 'Test Vendor'
            )
        } else if (OperatingSystem.current().isWindows()) {            
            commandLine osAdaptiveCommand(
                'jpackage',
                '--app-image', "${buildDir}/jpackage/${project.name}",
                '--name', "$project.name",
                '--app-version', "${version}",            
                '--description', 'Test Description',      
                '--dest', "${buildDir}/jpackage",
                '--verbose',
                '--vendor', 'Test Vendor',               
                '--win-shortcut',
                '--win-menu',
                '--win-menu-group', 'Games',
                '--win-per-user-install',
                '--type', 'msi'            
            )
        }  else {
            println "*** Unknown ***"
        }

    }
}




